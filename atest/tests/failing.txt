*** Settings ***
Resource          resource.txt
Suite Setup       Start And Import Remote Library    failing.py
Suite Teardown    Stop Remote Library
Test Template     Correct failure should occur

*** Variables ***
${SOURCE}         [\\w: /\\\\]+failing.py

*** Test Cases ***
Generic exceptions
    Exception    My message
    AssertionError    Another message
    RuntimeError    Yet another message

Non-generic exceptions
    NameError    My message    NameError: My message
    ZeroDivisionError    Another message    ZeroDivisionError: Another message

Custom exceptions
    MyException    The message    MyException: The message

No message
    Exception    ${NONE}    Exception

Empty message
    Exception    ${EMPTY}    Exception

Multiline message
    Exception    Can\n\haz\nmultiple\n\lines?\n\n\nYezzz!!!\n

Non-ASCII message
    Exception    Hyv√§ \u2603!!

Non-ASCII bytes
    Exception    'Hyv\\xe4'    Hyv\uFFFD    evaluate=yes

Binary message
    Exception    \x00.\x01.\x02

Failure deeper
    [Documentation]    FAIL Finally failing
    [Template]    NONE
    Failure Deeper

Traceback
    [Documentation]  FAIL MyException: My error message
    ...    LOG 1:1 FAIL MyException: My error message
    ...    LOG 1:2 DEBUG REGEXP: Traceback \\(most recent call last\\):
    ...    \\s+File "${SOURCE}", line 15, in failure
    ...    \\s+raise exception\\(message\\)
    [Template]    NONE
    Failure    MyException    My error message

Traceback with multiple entries
    [Documentation]  FAIL Finally failing
    ...    LOG 1:1 FAIL Finally failing
    ...    LOG 1:2 DEBUG REGEXP: Traceback \\(most recent call last\\):
    ...    \\s+File "${SOURCE}", line 21, in failure_deeper
    ...    \\s+self.failure_deeper\\(rounds-1\\)
    ...    \\s+File "${SOURCE}", line 21, in failure_deeper
    ...    \\s+self.failure_deeper\\(rounds-1\\)
    ...    \\s+File "${SOURCE}", line 20, in failure_deeper
    ...    \\s+raise RuntimeError\\('Finally failing'\\)
    [Template]    NONE
    Failure Deeper    rounds=3

*** Keywords ***
Correct failure should occur
    [Arguments]    ${exception}    ${message}    ${expected}=    ${evaluate}=
    ${expected} =    Set Variable If    """${expected}"""
    ...    ${expected}    ${message}
    Run Keyword And Expect Error    ${expected}
    ...    Failure    ${exception}    ${message}    ${evaluate}
