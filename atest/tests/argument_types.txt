*** Settings ***
Resource          resource.txt
Suite Setup       Start And Import Remote Library    arguments.py
Suite Teardown    Stop Remote Library
Test Template     Argument Should Be Correct

*** Test Cases ***
Empty string
    ''

ASCII string
    u'Hello, world!'
    'Hello, world!'

Non-ASCII String
    u'Hyv\\xe4'
    u'\\u2603'

Binary
    '\\x00\\x01'

Integer
    42
    -1
    0

Float
    3.14
    -0.1e10
    0.0

Boolean
    True
    False

None
    [Documentation]    None is converted to empty string because it is not supported by all XML-RPC versions.
    None    ''

Arbitrary Object
    [Documentation]    Arbitrary objects cannot be transferred over XML-RPC and thus only their string presentation is used
    [Template]    NONE
    Remote.Argument Should Be Correct    ${MyObject()}    '<MyObject>'

List
    \[]
    \['Hei', u'\\xe4iti', 63, True]
    \[None]    ['']
    \[[], [1, 2], [[True], False], 'xxx']

Tuple
    [Documentation]    Tuples are converted to lists
    ()    []
    ('Hei', u'\\xe4iti', 63, (), None)    ['Hei', u'\\xe4iti', 63, [], '']

Dictionary
    {}
    {'a': 1, u'\\xe4': 2, 'nested': {'k': 'v'}}
    {'x': None}    {'x': ''}

Dictionary With Non-String Keys
    [Documentation]    XML-RPC supports only strings as keys so must convert them
    {42: 42, True: False, None: None}    {'42': 42, 'True': False, '': ''}


*** Keywords ***
Argument Should Be Correct
     [Arguments]    ${argument}    ${expected}=
     ${expected} =    Set Variable If    """${expected}""" != ""
     ...    ${expected}    ${argument}
     ${argument} =    Evaluate    ${argument}
     Remote.Argument Should Be Correct    ${argument}    ${expected}
