*** Settings ***
Resource          resource.txt
Suite Setup       Start And Import Remote Library    failures.py
Suite Teardown    Stop Remote Library
Test Template     Correct failure should occur

*** Test Cases ***
Generic exceptions
    Exception    My message
    AssertionError    Another message
    RuntimeError    Yet another message

Non-generic exceptions
    NameError    My message    NameError: My message
    ZeroDivisionError    Another message    ZeroDivisionError: Another message

Custom exceptions
    MyException    The message    MyException: The message

No message
    Exception    ${NONE}    Exception

Empty message
    Exception    ${EMPTY}    Exception

Multiline message
    Exception    Can\n\haz\n4\n\lines?\n\n\nYezzz!!!\n

Non-ASCII message
    Exception    Hyv√§ \u2603!!

Non-ASCII bytes
    Exception    'Hyv\\xe4'    Hyv\uFFFD    evaluate=yes

Failure deeper
    [Documentation]    FAIL Finally failing
    [Template]    NONE
    Failure Deeper

*** Keywords ***
Correct failure should occur
    [Arguments]    ${exception}    ${message}    ${expected}=    ${evaluate}=
    ${expected} =    Set Variable If    """${expected}""" != ""
    ...    ${expected}    ${message}
    Run Keyword And Expect Error    ${expected}
    ...    Failure    ${exception}    ${message}    ${evaluate}
