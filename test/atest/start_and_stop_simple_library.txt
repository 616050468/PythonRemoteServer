*** Settings ***
Documentation
...    Tests in this suite have two different purposes:
...    1) Starting, importing and stopping remote libraries using 'Start Process', 'Import Library', and 'Stop Remote Server'.
...    2) Using keywords from a library that does not have get_kw_args/doc methods and that returns only a minimal result dictionary.
Library         Operating System

*** Variables ***
${SIMPLE PORT}        8271
${LIBRARY STARTED}    no

*** Test Cases ***
Remote keywords do not exist if remote server is not running
    [Documentation]    FAIL No keyword with name 'KW 1' found.
    KW 1    Does not work yet

Start And Import Remote Library
    Start Process    python ${CURDIR}${/}simplelibrary.py ${SIMPLE PORT}
    Import Library    Remote    localhost:${SIMPLE PORT}
    Set Suite Variable    ${LIBRARY STARTED}    yes
    KW 1    Now this works!

Keywords From Simple Library
    [Documentation]  FAIL My error message
    ${ret} =    KW 1
    Should Be Equal    ${ret}    ${EMPTY}
    ${ret} =    KW 1    Hello  world!
    Should Be Equal    ${ret}    Hello world!
    KW 2    My  error    message

Stop Remote Library
    [Documentation]  FAIL Connection to remote server broken: Connection refused
    Should Be Equal    ${LIBRARY STARTED}    yes
    Stop Remote Server
    ${out} =    Read Process Output
    Log    ${out}
    KW 1    This won't work anymore

